
/**
 * Common Gradle script configuration for bundling projects for standalone
 * download distributions.
 * 
 * @author Emanuel Rabina
 */

project.defaultTasks 'bundle'

configurations {
	bundle
}

/**
 * Create the source file archive (ZIP).
 */
task sourcesZip(
	description: 'Create a ZIP archive of all the project source files',
	type: Zip) {

	doFirst {
		baseName = "${project.artifact}"
	}
	from sourceSets.main.allSource
	destinationDir = file("${project.buildDir}/src")
	classifier = 'src'
}

/**
 * Include source and class JARs in the download bundle.
 */
artifacts {
	bundle jar, sourcesZip
}

/**
 * Creates the distribution archive for standalone downloads, a ZIP file
 * containing: the project JAR, source ZIP, folder containing all required
 * dependencies, and the license and readme files.
 */
task bundle(
	description: 'Create the overall distribution archive',
	dependsOn: [jar, javadoc, sourcesZip],
	type: Zip) {

	doFirst {
		archiveName = "${project.name} ${project.version}.zip"
	}
	from(configurations.bundle.artifacts.files)
	from(configurations.compile) {
		into 'Libraries'
	}
	from(javadoc.destinationDir) {
		into 'Javadoc'
	}
	from('.') {
		include 'LICENSE.txt'
		include 'README.md'
	}
	into project.name
}
