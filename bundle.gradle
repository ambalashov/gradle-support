
/**
 * Common Gradle script configuration for bundling projects for standalone
 * download distributions.
 * 
 * @author Emanuel Rabina
 */

project.defaultTasks 'bundle'

configurations {
	bundle
}

/**
 * Download dependency JARs.
 */
task downloadDependencies(
	description: 'Download all dependencies required by the project',
	type: Sync) {

	ext.dependenciesDir = file("${project.buildDir}/dependencies")
//	outputs.dir = dependenciesDir
	from configurations.compile
	into dependenciesDir
}

/**
 * Create the source file archive (ZIP).
 */
task sourcesZip(
	description: 'Create a ZIP archive of all the project source files',
	type: Zip) {

//	outputs.file = "${project.buildDir}/src/${archiveName}"
	from sourceSets.main.allSource
	destinationDir = file("${project.buildDir}/src")
	classifier = 'src'
}

/**
 * Include source and class JARs in the download bundle.
 */
artifacts {
	bundle jar, sourcesZip
}

/**
 * Creates the distribution archive for standalone downloads, a ZIP file
 * containing: the project JAR, source ZIP, folder containing all required
 * dependencies, and the license and readme files.
 */
task bundle(
	description: 'Create the overall distribution archive',
	dependsOn: [/*downloadDependencies,*/ jar, javadoc, sourcesZip],
	type: Zip) {

	archiveName = "${project.name} ${project.version}.zip"

	from(configurations.bundle)
	from(configurations.compile) {
		into 'Libraries'
	}
	from(javadoc.destinationDir) {
		into 'Javadoc'
	}
	from('.') {
		include 'LICENSE.txt'
		include 'README.md'
	}
	into project.name
}
