
/**
 * Common Gradle build script to make use of JAXB's XJC tool and any XJC
 * plugins used throughout my projects.
 * 
 * @author Emanuel Rabina
 */

configurations {
	xjc
}

dependencies {
	xjc(
		'javax.xml.bind:jaxb-api:2.2.6',
		'com.sun.xml.bind:jaxb-impl:2.2.6',
		'com.sun.xml.bind:jaxb-xjc:2.2.6',
		'jaxb:activation:1.0.2',
		'javax.xml.bind:jsr173_api:1.0',

		// Use the XJC taks from the JAXB2 Commons project to allow plugin use
		'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.6.3',
	)
}

sourceSets {
	main {
		resources {
			srcDir 'Schema'
		}
	}
}

/**
 * Get the latest modification time of the files in the file collection.
 * 
 * @param files
 */
def latestModificationTime(files) {

	def modificationTime = 0;
	files.each { file ->
		if (modificationTime < file.lastModified()) {
			modificationTime = file.lastModified()
		}
	}
	return modificationTime;
}

/**
 * Task to create Java class files from XML schemas.
 */
task xjc(
	description: 'XML schema to Java compiler',
	dependsOn: JavaPlugin.PROCESS_RESOURCES_TASK_NAME) {

	ext.schemaDir = 'Schema'
	ext.depends   = ['**/*.xsd']
	ext.outputDir = 'Java'
	ext.produces  = []
	ext.args      = []

	onlyIf {
		def schemaModifiedTime = latestModificationTime(fileTree(dir: schemaDir, includes: depends))
		def outputModifiedTime = latestModificationTime(fileTree(dir: outputDir, includes: produces))
		return schemaModifiedTime > outputModifiedTime
	}
	doLast {
		ant.taskdef(
			name: 'xjc',
			classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task',
			classpath: configurations.xjc.asPath)
		ant.xjc(
			destDir: outputDir,
			extension: 'true') {
			schema(dir: schemaDir, includes: includes)
			args.each { val ->
				arg(value: val)
			}
		}
	}
}
compileJava.dependsOn(xjc)
