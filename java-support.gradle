
/**
 * Common Gradle script configuration for Java projects and tasks.
 * 
 * @author Emanuel Rabina
 */

project.ext.artifact = ''

sourceSets {
	main {
		java {
			srcDir 'Java'
		}
		resources {
			srcDir 'Resources'
		}
		output.classesDir = 'Classes'
	}
	test {
		java {
			srcDir 'Tests'
		}
		output.classesDir = 'Classes'
	}
}

/**
 * Create a list of all immediate dependencies.
 *
 * @return Comma-seperated list of dependencies.
 */
def listDependencies() {
	def deps = '';
	configurations.compile.dependencies.each {
		deps += "${it.name}-${it.version}.jar, "
	}
	return !deps.isEmpty() ? deps.substring(0, deps.length() - 2) : deps;
}

/**
 * Reconfigure the JAR task to create a manifest file with some extra
 * information.
 */
jar {
	// Workaround for early evaluation of the archive name
	doFirst {
		baseName = "${project.artifact}"
	}

	exclude('**/tests/*')
	includeEmptyDirs = false
	manifest {
		attributes 'Class-Path': "${ -> listDependencies() }"
	}
}
